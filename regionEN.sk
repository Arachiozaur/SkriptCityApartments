#CityApartments 1.3 by Arachiozaur
#If you are using the code for own purposes, add me in the credits.
#------------------Configuration start------------------
options:
	cityprefix: &6[&4&lCityApartments&6]&r&f #Main commands prefix
	debugprefix: &4&l[&r&4&lCA_DEBUG&l]&r&f  #Debuging prefix
	world: world                             #A world in which there are regions specified on CA signs
	loadmessage: true                        #Send message when script is loaded, true or false
	sellinghomes: true                       #Can players (including admins) sell homes?
	city.managing: true                      #Can players (including admins) manage (add or remove members) their homes? 
	city.managing.paying: true                  #Do players have to pay for managing (adding or removing members) their homes?
	city.managing.addprice: 1                #Payment for adding members to home, if city.managing.pay is true.
	city.managing.removeprice: 1             #Payment for removing members from home, if city.managing.pay is true.
	interval: true                           #Should interval between using player commands be enabled?
	cmdinterval: 2 seconds                   #Interval between using player commands in seconds, default is 2 seconds.
#------------------Configuration end------------------

#------------------Variables configuration start------------------
#DO NOT MODIFY IT, UNLESS YOU ARE SURE YOU KNOW WHAT YOU ARE DOING
variables:
	cmd.%player% = true
#------------------Variables configuration end------------------

#------------------Script code start------------------
on script load:
	if {@loadmessage} is true:
		send "&f[&6Skript&f] &r{@cityprefix} &6enabled!" to the console
command /cityadmin <text> [<player>] [<text>]:
	permission: skript.cityapartments.admin
	trigger:
		if argument 1 is "stats":
			send "{@cityprefix} There are %{city.stats}% CA signs." to player
		if argument 1 is "signlist":
			send "%{signs::*}%" to player
		if argument 1 is "clearstats":
			set {city.stats} to 0
			clear {signs::*}
			send "{@cityprefix} Sign stats cleared."
		if argument 1 is "help":
			send "{@cityprefix}" 
			send "" to player
			send "&2Sign usage&f:" to player
			send "&cLine 1: &fcity" to player
			send "&cLine 2: &f[region]" to player
			send "&cLine 3: &f[price]" to player
			send "&2Commands:" to player
			send "&c /cityadmin debug on &fand &c/cityadmin debug off &f- Debug mode toggling" to player
			send "&c /cityadmin clear <player> &f- player's home reseting" to player
			send "&c /cityadmin stats &f- number of all CA signs." to player
			send "&c /cityadmin signlist &f- list of all CA signs." to player
			send "&c /cityadmin clearstats &f- list of all CA signs." to player
			send "&c /cityadmin signclear &f- clears all CA signs. &cUse with caution&f." to player		
		if argument 1 is "debug":
			if argument 3 is "on":
				set {city.debug} to true
				send "{@debugprefix} Debug is on." to player
			if argument 3 is "off":
				set {city.debug} to false
				send "{@debugprefix} Debug is false." to player
		if argument 1 is "clear":
			if {@sellinghomes} is true:
				set {city.limit.%argument 2%} to false
				execute console command "rg removeowner %{region.%argument 2%}% %argument 2% -w {@world}"
				clear {region.%argument 2%}
				send "{@cityprefix} %argument 2%'s home and his home limit has been set to &afalse&f."
			else:
				send "{@cityprefix} &cReseting homes is disabled."
		if argument 1 is "signclear":
			loop {signs::*}:
				set block at location of loop-value to air
			clear {signs::*}
			send "{@cityprefix} All signs removed." to player
		if argument 1 is "intervalreset":
			set {cmd.%argument 2%} to true
			send "{@cityprefix} {cmd.%player%} has been set to true."
command /city <text> [<player>]:
	permission: skript.cityapartments.player
	trigger:
		if argument 1 is "help":
			send "{@cityprefix}"
			send "&2Commands:"
			send "&c/city test &f- Checking your home status."
			send "&c/city add <player> &f- Add player to your home."
			send "&c/city remove <player> &f- Remove player from your home."
		if argument 1 is "test":
			send "{@cityprefix} Your region is &c%{region.%player%}%&f."
			if {city.limit.%player%} is true:
				send "{@cityprefix} Your home limit is &ctrue&f, you cannot buy apartments."
			else:
				send "{@cityprefix} Your home limit is &afalse&f, you can buy apartments."
		if argument 1 is "add":
			if {@city.managing} is true:
				if {@city.managing.paying} is true:
					execute console command "eco take %player% {@city.managing.addprice}"
					send "{@cityprefix} You have paid {@city.managing.addprice} for adding player &c%argument 2% &fto your home."
				execute console command "rg addmember %{region.%player%}% %argument 2% -w {@world}"
			else:
				send "{@cityprefix} Sorry, but home managing is disabled."
		if argument 1 is "remove":
			if {@city.managing} is true:
				if {@city.managing.paying} is true:
					execute console command "eco take %player% {@city.managing.removeprice}"
					send "{@cityprefix} You have paid {@city.managing.removeprice} for removing player &c%argument 2% &ffrom your home."
				execute console command "rg removemember %{region.%player%}% %argument 2% -w {@world}"
			else:
				send "{@cityprefix} Sorry, but home managing is disabled."
		if argument 1 is "info":
			if {@interval} is true:
				if {city.debug} is true:
					message "{@debugprefix} Interval is true." to player
				if {cmd.%player%} is true:
					if {city.debug} is true:
						message "{@debugprefix} {cmd.%player%} is true." to player
					execute console command "pex user %player% add worldguard.region.info.* {@world}"
					make player execute command "rg info %{region.%player%}%"
					execute console command "pex user %player% remove worldguard.region.info.* {@world}"
					set {cmd.%player%} to false
					wait {@cmdinterval}
					set {cmd.%player%} to true
				if {cmd.%player%} is false:
					send "{@cityprefix} Don't spam!"
					stop
			if {@interval} is false:
				if {city.debug} is true:
					message "{@debugprefix} Interval is false." to player
				execute console command "pex user %player% add worldguard.region.info.* {@world}"
				make player execute command "rg info %{region.%player%}%"
				execute console command "pex user %player% remove worldguard.region.info.* {@world}"
on sign change:
	if line 1 is "city":
		if player is op:
			set line 1 to "&6[CITY]"
			add 1 to {city.stats}
			if {city.debug} is true:
				message "{@debugprefix} Sign placed." to player
			if player does not have permission "skript.cityapartments.make":
				message "{@cityprefix} You don't have permission &4skript.city.sign &fto make a &aCityApartments &fsign."
				cancel event
				stop
			if line 2 is empty:
				message "{@cityprefix} Second line is not set."
				cancel event
				stop
			if line 3 is empty:
				message "{@cityprefix} Third line is not set."
				cancel event
				stop
			send "{@cityprefix} Sign succesfully created!"
		else:
			send "{@cityprefix} You don't have permission to make CA signs!"
on break of sign:
	if line 1 is "&4[Apartment]" or "&6[CITY]":
		if player is op:
			send "{@cityprefix} You have destroyed CityApartments sign!"
			remove 1 from {city.stats}
on rightclick on a sign:
	if line 1 of the clicked block is "&6[CITY]":
		if {city.debug} is true:
			message "{@debugprefix} Buy sign hit." to player
		if {city.debug} is true:
			message "{@debugprefix} Clicked sign's line 1 is city." to player
		if player has permission "skript.cityapartments.buy":
			if {city.debug} is true:
				message "{@debugprefix} Player has permission." to player
			set {_price} to line 3 of the clicked block
			if player's money is less than {_price}:
				send "{@cityprefix} You don't have enough money."
				stop
			else:
				cancel event
		if {city.limit.%player%} is true:
			send "{@cityprefix} You have reached your apartment limit!"
			cancel event
		else:
			set {region.%player%} to line 2 of clicked block
			add location of clicked block to {signs::*}
			send "{@cityprefix} You have bought an apartment &2%line 2%&f for &c%line 3%&f $."
			execute console command "rg addowner %line 2% %player% -w {@world}"
			execute console command "eco take %player% %line 3%"
			set line 1 of the clicked block to "&4[Apartment]"
			set line 2 of the clicked block to "%player%"
			set line 3 of the clicked block to " "
			set {city.limit.%player%} to true
			stop
	else:
		stop trigger
on rightclick on a sign:
	if line 1 of the clicked block is "&4[Apartment]":
		send "{@cityprefix} This apartment is occupied."
#------------------Script code end------------------